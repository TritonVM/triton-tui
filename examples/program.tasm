//! Demonstrates use and functionality of the Triton VM TUI.
//!
//! Serves as the tutorial: execution intentionally fails at first, hopefully
//! encouraging you to play around. :) The inline comments should make matters trivial.
//!
//! Press `h` in Triton VM TUI to show the help screen.

// Let's start!
push 3
hint loop_counter = stack[0]        // Current stack can be annotated to help debugging.
break

call my_loop
hint numbers: array = stack[1..4]   // Annotations also work with ranges.

call check_result
call write_to_memory
call read_from_secret_input
call sponge_instructions
halt

my_loop:
    dup 0 push 0 eq skiz return
    read_io 3                       // call with `--input ./public_input.txt`
    mul mul
    break
    dup 0 write_io 1
    swap 1 push -1 add recurse

check_result:                       // Try enabling
    // pop 1                        // <- this line for
    assert                          // <- this assertion to work.
    return                          // Hot reload & reset of the VM is `r`.

write_to_memory:
    push 42
    write_mem 2                     // You can inspect memory with `m`.
    pop 1
    return

read_from_secret_input:
    divine 3                        // flag `--non-determinism ./non_determinism.json`
    mul mul
    swap 5
    divine_sibling pop 5
    divine_sibling hash
    return

sponge_instructions:
    sponge_init                     // Show the Sponge state with `t,s`.
    sponge_squeeze
    return
